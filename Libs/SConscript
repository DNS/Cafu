import os, sys, platform

Import('env', 'compiler')


env.StaticLibrary(
    target="cfsLib",
    source=Split("""Bitmap/Bitmap.cpp Bitmap/jdatasrc.cpp
                    ConsoleCommands/ConVar.cpp ConsoleCommands/ConFunc.cpp
                    ConsoleCommands/ConsoleInterpreterImpl.cpp ConsoleCommands/ConsoleInterpreter_LuaBinding.cpp
                    ConsoleCommands/Console.cpp ConsoleCommands/Console_Lua.cpp ConsoleCommands/ConsoleComposite.cpp ConsoleCommands/ConsoleStdout.cpp ConsoleCommands/ConsoleStringBuffer.cpp ConsoleCommands/ConsoleWarningsOnly.cpp ConsoleCommands/ConsoleFile.cpp
                    Fonts/Font.cpp Fonts/FontTT.cpp
                    FileSys/FileManImpl.cpp FileSys/FileSys_LocalPath.cpp FileSys/FileSys_ZipArchive_GV.cpp FileSys/File_local.cpp FileSys/File_memory.cpp FileSys/Password.cpp
                    MainWindow/glfwLibrary.cpp MainWindow/glfwMainWindow.cpp MainWindow/glfwMonitor.cpp MainWindow/glfwWindow.cpp
                    MapFile.cpp
                    Models/Loader.cpp Models/Loader_ase.cpp Models/Loader_cmdl.cpp Models/Loader_dlod.cpp Models/Loader_dummy.cpp Models/Loader_lwo.cpp Models/Loader_md5.cpp
                    Models/Loader_mdl.cpp Models/Loader_mdl_hl1.cpp Models/Loader_mdl_hl2.cpp Models/Loader_mdl_hl2_vtf.cpp Models/AnimExpr.cpp Models/AnimPose.cpp
                    Models/Model_cmdl.cpp Models/ModelManager.cpp
                    Network/Network.cpp Network/State.cpp ParticleEngine/ParticleEngineMS.cpp PlatformAux.cpp Terrain/Terrain.cpp
                    TextParser/TextParser.cpp
                    Plants/Tree.cpp Plants/PlantDescription.cpp Plants/PlantDescrMan.cpp
                    Util/Util.cpp
                    Win32/Win32PrintHelp.cpp
                    DebugLog.cpp PhysicsWorld.cpp TypeSys.cpp UniScriptState.cpp Variables.cpp VarVisitorsLua.cpp""")+
           Glob("GameSys/*.cpp")+Glob("GameSys/HumanPlayer/*.cpp")+
           Glob("GuiSys/*.cpp")+
           Glob("Math3D/*.cpp"),
    CPPPATH=env['CPPPATH'] + ["#/ExtLibs/bullet/src", "#/ExtLibs/glfw/include", "#/ExtLibs/lua/src", "#/ExtLibs/lwo", "#/ExtLibs/jpeg"])



# TODO: Either integrate this into cfsLib above or integrate the above model loaders here.
envModelLoaders = env.Clone()

if sys.platform == "win32":
    # The next two lines are mirrored in ./SConscript.
    fbx_lib_path = "#/ExtLibs/fbx/lib/" + \
        {"vc8": "vs2005", "vc9": "vs2008", "vc10": "vs2010", "vc11": "vs2012", "vc12": "vs2013", "vc14": "vs2015"}[compiler]

    if os.path.exists(Dir("#/ExtLibs/fbx/include").abspath) and os.path.exists(Dir(fbx_lib_path).abspath):
        envModelLoaders.Append(CPPDEFINES=["HAVE_FBX_SDK"])
        envModelLoaders.Append(CPPPATH=["#/ExtLibs/fbx/include"])
elif sys.platform.startswith("linux"):
    if os.path.exists(Dir("#/ExtLibs/fbx/include").abspath):
        envModelLoaders.Append(CPPDEFINES=["HAVE_FBX_SDK"])
        envModelLoaders.Append(CPPPATH=["#/ExtLibs/fbx/include"])

envModelLoaders.StaticLibrary("ModelLoaders", ["Models/Loader_fbx.cpp"])



env.StaticLibrary(
    target="ClipSys",
    source=Split("""ClipSys/CollisionModel_static.cpp ClipSys/CollisionModel_static_BulletAdapter.cpp ClipSys/CollisionModelMan_impl.cpp
                    ClipSys/ClipModel.cpp ClipSys/ClipWorld.cpp ClipSys/TraceSolid.cpp"""),
    CPPPATH=env['CPPPATH']+["#/ExtLibs/bullet/src"])



env.StaticLibrary(
    target="MatSys",
    source=Split("""MaterialSystem/Expression.cpp MaterialSystem/MapComposition.cpp MaterialSystem/Material.cpp
                    MaterialSystem/MaterialManagerImpl.cpp MaterialSystem/Renderer.cpp MaterialSystem/TextureMap.cpp"""))



env.StaticLibrary(
    target="SoundSys",
    source=Split("""SoundSystem/SoundShaderManagerImpl.cpp SoundSystem/SoundShader.cpp"""))



env.StaticLibrary(
    target="SceneGraph",
    source=Split("""SceneGraph/_aux.cpp SceneGraph/BezierPatchNode.cpp SceneGraph/BspTreeNode.cpp SceneGraph/FaceNode.cpp
                    SceneGraph/LightMapMan.cpp SceneGraph/Node.cpp SceneGraph/SHLMapMan.cpp SceneGraph/TerrainNode.cpp
                    SceneGraph/PlantNode.cpp SceneGraph/ModelNode.cpp"""))



envRenderers = env.Clone()

MatSys_CommonObjectsList = envRenderers.SharedObject(["MaterialSystem/Common/DepRelMatrix.cpp", "MaterialSystem/Common/OpenGLState.cpp", "MaterialSystem/Common/OpenGLEx.cpp"])

if sys.platform.startswith("linux"):
    envRenderers.Append(LINKFLAGS=["Libs/MaterialSystem/Common/linker-script"])



if sys.platform=="win32":
    LibList=Split("cfsLib png cfs_jpeg z MatSys opengl32.lib glu32.lib")
else:
    LibList=["GLU"]

envRenderers.SharedLibrary(
    target="MaterialSystem/RendererARBprogs",
    source=[os.path.join("MaterialSystem/RendererARBprogs", x) for x in Split("""
               RendererARBprogs.cpp RendererImpl.cpp RenderMaterial.cpp Shader.cpp TextureMapImpl.cpp

               Shaders/A_Terrain.cpp Shaders/A_Diff.cpp Shaders/A_Diff_Luma.cpp Shaders/A_Diff_Norm.cpp Shaders/A_Diff_Norm_Luma.cpp
               Shaders/A_Diff_Light.cpp Shaders/A_Diff_Light_Luma.cpp Shaders/A_Diff_Light_Norm.cpp Shaders/A_Diff_Light_Norm_Spec.cpp
               Shaders/A_Diff_Light_Norm_Luma.cpp Shaders/A_Diff_Light_Norm_Luma_Spec.cpp
               Shaders/A_SkyDome.cpp Shaders/A_Solid.cpp Shaders/A_WaterCubeReflect.cpp
               Shaders/L_Diff.cpp Shaders/L_Diff_Spec.cpp Shaders/L_Diff_Norm.cpp Shaders/L_Diff_Norm_Spec.cpp
               Shaders/StencilShadowVolumes.cpp
               Shaders/_CommonHelpers.cpp""")] +
           MatSys_CommonObjectsList,
    LIBS=LibList)



if not platform.machine().startswith("arm"):
    LibPath="#/ExtLibs/Cg/lib"
    if sys.platform=="win32":
        LibList=Split("cfsLib png cfs_jpeg z MatSys opengl32.lib glu32.lib cg cgGL")
        if envRenderers["TARGET_ARCH"]!="x86": LibPath += ".x64"
    elif sys.platform.startswith("linux"):
        LibList=Split("Cg CgGL GLU")
        if platform.machine()=="x86_64": LibPath += ".x64"
    else:
        LibList=[]

    envRenderers.SharedLibrary(
        target="MaterialSystem/RendererCgARB1",
        source=[os.path.join("MaterialSystem/RendererCgARB1", x) for x in Split("""
                   RendererCgARB1.cpp RendererImpl.cpp RenderMaterial.cpp Shader.cpp TextureMapImpl.cpp

                   Shaders/A_Terrain.cpp Shaders/A_Diff.cpp Shaders/A_Diff_Luma.cpp Shaders/A_Diff_Norm.cpp Shaders/A_Diff_Norm_Luma.cpp
                   Shaders/A_Diff_Light.cpp Shaders/A_Diff_Light_Luma.cpp Shaders/A_Diff_Light_Norm.cpp Shaders/A_Diff_Light_Norm_Luma.cpp
                   Shaders/A_SkyDome.cpp Shaders/A_Solid.cpp Shaders/A_WaterCubeReflect.cpp Shaders/L_Diff.cpp Shaders/L_Diff_Spec.cpp
                   Shaders/L_Diff_Norm.cpp Shaders/L_Diff_Norm_Spec.cpp Shaders/StencilShadowVolumes.cpp Shaders/_CommonCgHelpers.cpp""")] +
               MatSys_CommonObjectsList,
        CPPPATH=env['CPPPATH']+["#/ExtLibs/Cg/include"],
        LIBPATH=env['LIBPATH']+[LibPath],
        LIBS=LibList)



if not platform.machine().startswith("arm"):
    LibPath="#/ExtLibs/Cg/lib"
    if sys.platform=="win32":
        LibList=Split("cfsLib png cfs_jpeg z MatSys opengl32.lib glu32.lib cg cgGL")
        if envRenderers["TARGET_ARCH"]!="x86": LibPath += ".x64"
    elif sys.platform.startswith("linux"):
        LibList=Split("Cg CgGL GLU")
        if platform.machine()=="x86_64": LibPath += ".x64"
    else:
        LibList=[]

    envRenderers.SharedLibrary(
        target="MaterialSystem/RendererCgNV2X",
        source=[os.path.join("MaterialSystem/RendererCgNV2X", x) for x in Split("""
                   RendererCgNV2X.cpp RendererImpl.cpp RenderMaterial.cpp Shader.cpp TextureMapImpl.cpp

                   Shaders/A_Terrain.cpp Shaders/A_Diff.cpp Shaders/A_Diff_Luma.cpp Shaders/A_Diff_Norm.cpp Shaders/A_Diff_Norm_Luma.cpp
                   Shaders/A_Diff_Light.cpp Shaders/A_Diff_Light_Luma.cpp Shaders/A_Diff_Light_Norm.cpp Shaders/A_Diff_Light_Norm_Luma.cpp
                   Shaders/A_SkyDome.cpp Shaders/A_Solid.cpp Shaders/A_WaterCubeReflect.cpp Shaders/L_Diff.cpp Shaders/L_Diff_Spec.cpp
                   Shaders/L_Diff_Norm_oSpec.cpp Shaders/StencilShadowVolumes.cpp Shaders/_CommonCgHelpers.cpp""")] +
               MatSys_CommonObjectsList,
        CPPPATH=env['CPPPATH']+["#/ExtLibs/Cg/include"],
        LIBPATH=env['LIBPATH']+[LibPath],
        LIBS=LibList)



if sys.platform=="win32":
    LibList=Split("cfsLib MatSys")
else:
    LibList=[]

envRenderers.SharedLibrary(
    target="MaterialSystem/RendererNull",
    source=[os.path.join("MaterialSystem/RendererNull", x) for x in Split("""RendererNull.cpp RendererImpl.cpp TextureMapImpl.cpp""")],
    LIBS=LibList)



if sys.platform=="win32":
    LibList=Split("cfsLib png cfs_jpeg z MatSys opengl32.lib glu32.lib")
else:
    LibList=["GLU"]

envRenderers.SharedLibrary(
    target="MaterialSystem/RendererOpenGL12",
    source=[os.path.join("MaterialSystem/RendererOpenGL12", x) for x in Split("""
               RendererImpl.cpp RendererOpenGL12.cpp RenderMaterial.cpp Shader.cpp TextureMapImpl.cpp

               Shaders/A_Diff_Light_Fog.cpp Shaders/A_Diff_Light_oLuma.cpp Shaders/A_Diff_nLight_oLuma.cpp Shaders/A_SkyDome.cpp
               Shaders/A_Solid.cpp Shaders/A_Terrain.cpp Shaders/A_WaterCubeReflect.cpp Shaders/L_Diff_oSpec.cpp Shaders/StencilShadowVolumes.cpp""")] +
           MatSys_CommonObjectsList,
    LIBS=LibList)



envSoundSys = env.Clone()

if sys.platform.startswith("linux"):
    envSoundSys.Append(LINKFLAGS=["Libs/SoundSystem/Common/linker-script"])

envSoundSys.SharedLibrary(
    target="SoundSystem/SoundSysNull",
    source=[os.path.join("SoundSystem/SoundSysNull", x) for x in Split("SoundSysImpl.cpp DllInterface.cpp")],
    LIBS="SoundSys")



envSoundSys = env.Clone()
envSoundSys.Append(CPPPATH=['#/ExtLibs/fmod/api/inc'])

if sys.platform=="win32":
    envSoundSys.Append(LIBPATH=['#/ExtLibs/fmod/api/lib'])
    envSoundSys.Append(LIBS=Split("cfsLib SoundSys fmodvc"))
elif sys.platform.startswith("linux"):
    envSoundSys.Append(LINKFLAGS=["Libs/SoundSystem/Common/linker-script"])
    envSoundSys.Append(LIBPATH=['#/ExtLibs/fmod/api'])
    envSoundSys.Append(LIBS=["SoundSys", "fmod-3.75"])

# We have the FMod libraries only for 32-bit platforms...
if not platform.machine().startswith("arm"):
    if (sys.platform=="win32" and envSoundSys["TARGET_ARCH"]=="x86") or (sys.platform.startswith("linux") and platform.machine()!="x86_64"):
        envSoundSys.SharedLibrary(
            target="SoundSystem/SoundSysFMOD3",
            source=[os.path.join("SoundSystem/SoundSysFMOD3", x) for x in Split("""
                       SoundSysImpl.cpp DllInterface.cpp SoundImpl.cpp BufferManager.cpp
                       StaticBuffer.cpp StreamingBuffer.cpp Channel.cpp""")])



envSoundSys = env.Clone()
envSoundSys.Append(CPPPATH=['#/ExtLibs/freealut/include', '#/ExtLibs/mpg123/src/libmpg123', '#/ExtLibs/libvorbis/include', '#/ExtLibs/libogg/include'])

if sys.platform=="win32":
    envSoundSys.Append(CPPPATH=['#/ExtLibs/openal-win/include'])
    envSoundSys.Append(LIBS=Split("cfsLib SoundSys OpenAL32 alut mpg123 ogg vorbis vorbisfile"))
    if envSoundSys["TARGET_ARCH"]=="x86":
        envSoundSys.Append(LIBPATH=['#/ExtLibs/openal-win/libs/Win32'])
    else:
        envSoundSys.Append(LIBPATH=['#/ExtLibs/openal-win/libs/Win64'])
elif sys.platform.startswith("linux"):
    envSoundSys.Append(CPPPATH=['#/ExtLibs/openal-soft/include'])
    envSoundSys.Append(LIBS=Split("openal alut mpg123 vorbisfile vorbis ogg"))
    envSoundSys.Append(LINKFLAGS=["Libs/SoundSystem/Common/linker-script"])

envSoundSys.SharedLibrary(
    target="SoundSystem/SoundSysOpenAL",
    source=Glob("SoundSystem/Common/*.cpp")+
           Glob("SoundSystem/SoundSysOpenAL/*.cpp"))
